apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId rootProject.ext.applicationId
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
    }

    signingConfigs {
        debug {
            storeFile file("../keystore/debug_key.jks")
            storePassword "android"
            keyAlias "mocktrade"
            keyPassword "android"
        }

        release {
            storeFile file(System.getenv("com.balch.mocktrade.storeFile") ?: "../keystore/release_key.jks")
            storePassword System.getenv("com.balch.mocktrade.storePassword") ?: ""
            keyAlias System.getenv("com.balch.mocktrade.keyAlias") ?: ""
            keyPassword System.getenv("com.balch.mocktrade.keyPassword") ?: ""
        }
    }

    buildTypes {
        debug {
            // sign debug build with a common key to facilitate develop from multiple
            // computers w/o having to reinstall
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }

    packagingOptions{
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    wearApp project(':MockTradeWear')
    implementation project(':AppFramework')
    implementation project(':MockTradeShared')

    implementation "com.google.code.gson:gson:$gson"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_logging_xcepter"

    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit"
    implementation "io.reactivex.rxjava2:rxjava:$rxjava2"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid"

    implementation "androidx.lifecycle:lifecycle-extensions:$android_arch"
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:$android_arch"

    implementation "androidx.legacy:legacy-support-v4:$support"
    implementation "androidx.appcompat:appcompat:$support"
    implementation "androidx.recyclerview:recyclerview:$support"
    implementation "androidx.cardview:cardview:$cardview"
    implementation "com.google.android.material:material:$support"
    implementation "com.google.android.gms:play-services-wearable:$play_services"

    implementation "net.danlew:android.joda:$joda"


    // Testing-only dependencies

    // Force usage of support annotations in the test app, since it is internally used by the runner module.
    testImplementation "androidx.annotation:annotation:$support"
    testImplementation "androidx.test:runner:$support_test"
    testImplementation "androidx.test:rules:$support_test"
    testImplementation "org.mockito:mockito-core:$mockito"
}

